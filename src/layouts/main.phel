(ns app\layouts\main
  (:require phel\html :refer [html doctype])
  (:require app\partials\head)
  (:require app\partials\navbar)
  (:require app\partials\footer))

(defn white [request white-section]
  [:section {:class "white"}
   [:div {:class "container paper"}
    [:div {:class "row justify-content-center px-0 px-sm-3 px-md-5"}
     [:div {:class "col"}
      (navbar/render (get (get request :uri) :path))]]
    [:div {:class "row justify-content-center px-0 px-sm-3 px-md-5"}
     [:div {:class "col"} white-section]]]])

(defn black [black-section]
  [:section {:class "black"}
   [:div {:class "container"}
    [:div {:class "row justify-content-center px-0 px-sm-3 px-md-5"}
     [:div {:class "col"} black-section]]]])

(defn scripts [& [additional-scripts]]
  (list [:script {:src "/js/bundle.js"}]
        additional-scripts))

(defn body [request white-section black-section & [additional-scripts]]
  [:body
   (white request white-section)
   (black black-section)
   (footer/render)
   (scripts additional-scripts)])

(defn render [request sections & [additional-scripts]]
  (html
   (doctype :html5)
   [:html {:lang "en"}
    (head/render (get sections :title) (get sections :meta-description))
    (body request
          (:white sections)
          (:black sections)
          additional-scripts)]))