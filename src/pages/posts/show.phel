(ns app\pages\posts\show
  (:require app\layouts\main)
  (:require app\posts)
  (:require app\pages\page-not-found)
  (:require phel\html :refer [raw-string]))

(defn get-post-meta [post & [show-heading]]
  (let [show-heading (or show-heading true)]
    [:section @{:class "post-meta"}
     (if (= show-heading true)
       [:p @{:class "lead post-meta-heading"} (posts/get-title post)])
     [:p @{:class "post-meta-author"}
      [:small
       "Written by "
       [:b (posts/get-author post)]
       " on "
       [:b (posts/get-formatted-date post)]]]]))

(defn white [post]
  [:div
   [:h1 @{:class "display-1 pt-5 mt-5"} (posts/get-title post)]
   [:p @{:class "lead"} (posts/get-excerpt post)]
   (get-post-meta post @{:heading false})
   [:p [:a @{:href "/" :class "text-dark"} "home"] "/ blog"]])

(defn black [post]
  [:div @{:class "row"}
   [:div @{:class "col"}
    [:article @{:class "post-content"}
     (raw-string (posts/markdown-to-html (posts/get-body post)))]
    [:hr @{:style "width: 130px;" :class "mb-4"}]
    (get-post-meta post)]])

(defn render [request & [@{:year year :month month :day day :slug slug}]]
  (let [post (posts/get-by-slug year month day slug)]
    (if (nil? post)
      (page-not-found/render request)
      @{:status 200
        :body (main/render
               request
               @{:title (str (posts/get-title post) " | Mario Bašić")
                 :meta-description (posts/get-excerpt post)
                 :white (white post)
                 :black (black post)})})))